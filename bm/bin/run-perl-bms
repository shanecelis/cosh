#!/usr/bin/perl

use warnings;
use strict;

use JSON::XS qw(encode_json);
my $count = 10;

my ($perl_ver) =
    grep { /v5\./ }
        `perl -v`;
chomp $perl_ver;
$perl_ver =~ s/^.*\(v(5\..*?)\).*$/$1/;

sub run_cmd
{
    my ($cmd, $program, $lang, $ext) = @_;

    my ($fig) = `./bm/bin/$cmd $lang $program.$ext`;
    chomp $fig;
    return $fig;
}

if (not -e 'bm-file.txt') {
    my $res = system("bm/bin/generate-bm-file > bm-file.txt");
    if ($res != 0) {
        die "Unable to generate benchmark file";
    }
}

my @programs =
    map { s/\.pl//; $_ }
    map { chomp; $_ }
        `ls bm/programs/*.pl`;

my @perl_extras = qw(perl pl);
my $time = time();
my %bm_details = (
    other => { time => $time },
    perl  => { version => $perl_ver },
);
for my $program (@programs) {
    my %perl_details = (
        avg_time => run_cmd("avg-time $count", $program, @perl_extras),
        avg_inst => run_cmd("avg-perf instructions $count", $program, @perl_extras),
        avg_mem  => run_cmd("avg-perf syscalls:sys_enter_mmap,syscalls:sys_enter_brk 1",
                            $program, @perl_extras),
        max_rss  => run_cmd("max-rss", $program, @perl_extras),
    );

    my $pname = $program;
    $pname =~ s/.*\///;
    $bm_details{'programs'}->{$pname} = {
        perl => \%perl_details,
    };
}

unlink "perf.data";
unlink "perf.data.old";

print encode_json(\%bm_details);

1;
